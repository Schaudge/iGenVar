rule all:
    input:
        expand("results/parameter_benchmarks/plots/{parameter_name}.results.all.png",
               parameter_name="min_var_length"),
        expand("results/parameter_benchmarks/plots/{parameter_name}.results.all.png",
               parameter_name="max_var_length"),
        expand("results/parameter_benchmarks/plots/{parameter_name}.results.all.png",
               parameter_name="max_tol_inserted_length"),
        expand("results/parameter_benchmarks/plots/{parameter_name}.results.all.png",
               parameter_name="max_tol_deleted_length"),
        expand("results/parameter_benchmarks/plots/{parameter_name}.results.all.png",
               parameter_name="max_overlap"),
        expand("results/parameter_benchmarks/plots/{parameter_name}.results.all.png",
               parameter_name="partition_max_distance"),
        expand("results/parameter_benchmarks/plots/{parameter_name}.results.all.png",
               parameter_name="hierarchical_clustering_cutoff")

rule run_igenvar:
    input:
        bam = "data/long_reads/HG002.Sequel.10kb.pbmm2.hs37d5.whatshap.haplotag.RTG.10x.trio_sorted.bam"
    output:
        vcf = "results/parameter_benchmarks/{parameter_name}/{parameter_value}_output.vcf"
    shell:
        """
        ./build/iGenVar/bin/iGenVar -t 1 -j {input.bam} -o {output.vcf} --vcf_sample_name HG002 \
        --method cigar_string --method split_read -c 1 --min_qual 2 \
        --{wildcards.parameter_name} {wildcards.parameter_value}
        """

rule filter_vcf:
    input:
        vcf = "results/parameter_benchmarks/{parameter_name}/{parameter_value}_output.vcf"
    output:
        "results/parameter_benchmarks/{parameter_name}/{parameter_value}_output.min_qual_{min_qual}.vcf"
    shell:
        "bcftools view -i 'QUAL>={wildcards.min_qual}' {input.vcf} > {output}"

rule bgzip:
    input:
        "{name}.vcf"
    output:
        "{name}.vcf.gz"
    shell:
        "bgzip -c {input} > {output}"

rule tabix:
    input:
        "{name}.vcf.gz"
    output:
        "{name}.vcf.gz.tbi"
    shell:
        "tabix -p vcf {input}"

rule truvari:
    input:
        vcf = "results/parameter_benchmarks/{parameter_name}/{parameter_value}_output.min_qual_{min_qual}.vcf.gz",
        index = "results/parameter_benchmarks/{parameter_name}/{parameter_value}_output.min_qual_{min_qual}.vcf.gz.tbi"
    params:
        output_dir = "results/parameter_benchmarks/truvari/{parameter_name}/{parameter_value}_min_qual_{min_qual}"
    output:
        summary = "results/parameter_benchmarks/truvari/{parameter_name}/{parameter_value}_min_qual_{min_qual}/summary.txt"
    shell:
        """
        rm -rf {params.output_dir} && truvari bench -b data/truth_set/HG002_SVs_Tier1_v0.6.vcf.gz \
        -c {input.vcf} -o {params.output_dir} --passonly --includebed data/truth_set/HG002_SVs_Tier1_v0.6.bed -p 0 \
        &>> logs/truvari_output.log
        """

rule reformat_truvari_results:
    input:
        "results/parameter_benchmarks/truvari/{parameter_name}/{parameter_value}_min_qual_{min_qual}/summary.txt"
    output:
        "results/parameter_benchmarks/truvari/{parameter_name}/{parameter_value}_min_qual_{min_qual}/pr_rec.txt"
    threads: 1
    shell:
        """
        cat {input} | grep '\<precision\>\|\<recall\>' | tr -d ',' |sed 's/^[ \t]*//' | tr -d '\"' | tr -d ' ' | tr ':' '\t' \
        | awk 'OFS=\"\\t\" {{ print \"{wildcards.parameter_value}\", \"{wildcards.min_qual}\", $1, $2 }}' > {output}
        """

rule cat_truvari_results_full:
    input:
        input1 = expand("results/parameter_benchmarks/truvari/min_var_length/{parameter_value}_min_qual_{min_qual}/pr_rec.txt",
               parameter_value=[10, 30, 50, 100],                           # default: 30
               min_qual=range(1, 51)),
        input2 = expand("results/parameter_benchmarks/truvari/max_var_length/{parameter_value}_min_qual_{min_qual}/pr_rec.txt",
               parameter_value=[1000, 10000, 100000, 1000000],              # default: 100.000
               min_qual=range(1, 51)),
        input3 = expand("results/parameter_benchmarks/truvari/max_tol_inserted_length/{parameter_value}_min_qual_{min_qual}/pr_rec.txt",
               parameter_value=[1, 10, 100, 1000],                          # default: 50
               min_qual=range(1, 51)),
        input4 = expand("results/parameter_benchmarks/truvari/max_tol_deleted_length/{parameter_value}_min_qual_{min_qual}/pr_rec.txt",
               parameter_value=[1, 10, 100, 1000],                          # default: 50
               min_qual=range(1, 51)),
        input5 = expand("results/parameter_benchmarks/truvari/max_overlap/{parameter_value}_min_qual_{min_qual}/pr_rec.txt",
               parameter_value=[1, 10, 100, 1000],                          # default: 10
               min_qual=range(1, 51)),
        input6 = expand("results/parameter_benchmarks/truvari/partition_max_distance/{parameter_value}_min_qual_{min_qual}/pr_rec.txt",
               parameter_value=[10, 100, 1000, 10000],                      # default: 1000
               min_qual=range(1, 51)),
        input7 = expand("results/parameter_benchmarks/truvari/hierarchical_clustering_cutoff/{parameter_value}_min_qual_{min_qual}/pr_rec.txt",
               parameter_value= [0.2, 0.5, 1, 10],                          # default: 0.5
               min_qual=range(1, 51))
    output:
        summary1 = "results/parameter_benchmarks/truvari/min_var_length/all_results.txt",
        summary2 = "results/parameter_benchmarks/truvari/max_var_length/all_results.txt",
        summary3 = "results/parameter_benchmarks/truvari/max_tol_inserted_length/all_results.txt",
        summary4 = "results/parameter_benchmarks/truvari/max_tol_deleted_length/all_results.txt",
        summary5 = "results/parameter_benchmarks/truvari/max_overlap/all_results.txt",
        summary6 = "results/parameter_benchmarks/truvari/partition_max_distance/all_results.txt",
        summary7 = "results/parameter_benchmarks/truvari/hierarchical_clustering_cutoff/all_results.txt"
    shell:
        """
        cat {input.input1} > {output.summary1} && cat {input.input2} > {output.summary2} && \
        cat {input.input3} > {output.summary3} && cat {input.input4} > {output.summary4} && \
        cat {input.input5} > {output.summary5} && cat {input.input6} > {output.summary6} && \
        cat {input.input7} > {output.summary7}
        """

rule plot_pr_all_results:
    input:
        "results/parameter_benchmarks/truvari/{parameter_name}/all_results.txt"
    output:
        "results/parameter_benchmarks/plots/{parameter_name}.results.all.png"
    shell:
        "Rscript --vanilla Repos/iGenVar/test/benchmark/parameter_benchmarks/plot_all.R {input} {wildcards.parameter_name} {output}"
